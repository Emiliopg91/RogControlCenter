# Maintainer: Emiliopg91 <ojosdeserbio@gmail.com>
pkgname=rog-perf-tuner
pkgver=<version>
pkgrel=1
pkgdesc="RogPerfTuner - An utility to manage Asus Rog laptop performance and RGB lighting"
arch=('x86_64')
url="https://github.com/Emiliopg91/RogPerfTuner"
license=('GPL2')
source=("git+https://github.com/Emiliopg91/RogPerfTuner.git#tag=$pkgver")
sha256sums=('SKIP')
options=('!strip')
depends=(
  'asusctl'
  'coreutils'
  'fuse2'
  'hicolor-icon-theme'
  'hidapi'
  'libsecret'
  'libusb'
  'mbedtls'
  'power-profiles-daemon'
  'python'
  'python-pip'
  'qt5-base'
  'qt5-tools'
  'qt6-base'
  'qt6-charts'
  'qt6-svg'
  'scx-scheds'
  'upower'
)
makedepends=(
  'base-devel'
  'clang'
  'cmake'
  'git'
  'libsecret'
  'ninja'
  'npm'
  'pkgconf'
  'pnpm'
  'podman'
  'qtcreator'
  'unzip'
  'zip'
)

prepare() {
    if [[ ! -d "${HOME}/.local/share/pnpm" ]]; then
        echo "Setup for pnpm..."
        SHELL=/bin/bash pnpm setup
    fi

    cd "$srcdir/RogPerfTuner"
    git submodule update --init --recursive

    export CONTAINERS_STORAGE_CONF=$srcdir/containers-storage.conf
    cat > "$CONTAINERS_STORAGE_CONF" <<EOF
[storage]
driver = "vfs"
runroot = "/tmp/containers/runroot"
graphroot = "/tmp/containers/storage"
EOF
}

build() {
    export APPIMAGE_EXTRACT_AND_RUN=1
    unset SOURCE_DATE_EPOCH

    case "$ARCH_LEVEL" in
        x86-64-v3) export CXXFLAGS="-march=x86-64-v3" ;;
        x86-64-v4) export CXXFLAGS="-march=x86-64-v4" ;;
        zen4)      export CXXFLAGS="-march=znver4" ;;
        native)    export CXXFLAGS="-march=native" ;;
        ""|default|*) export CXXFLAGS="" ;;  # Por defecto
    esac

    echo "Using CXXFLAGS: $CXXFLAGS"

    cd "$srcdir/RogPerfTuner"
    IS_AURPKG=1 make release
}

package() {
    cd "$srcdir/RogPerfTuner/dist/appimage-fs"

    install -Dm755 ../RogPerfTuner.AppImage \
        "$pkgdir/usr/bin/rog-perf-tuner"

    install -d "$pkgdir/usr/share/applications"
    sed -e 's|Exec=usr/bin/RogPerfTuner|Exec=/usr/bin/rog-perf-tuner|' \
        -e 's|Icon=icon|Icon=rog-perf-tuner|' \
        rog-perf-tuner.desktop > "$pkgdir/usr/share/applications/rog-perf-tuner.desktop"

    install -Dm644 icon.svg \
        "$pkgdir/usr/share/icons/hicolor/scalable/apps/rog-perf-tuner.svg"
}
